# Query the pilot region for companies and the number of employees
# On 2025-02-06 (loaded database a few weeks older) there was only a single data point

PREFIX spatial: <http://jena.apache.org/spatial#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>

SELECT ?geom ?geomTooltip ?geomLabel ?x {
  VALUES ?searchGeom {
    "POLYGON ((6.475457372470345 51.7037981703763, 6.475457372470346 51.2481032780451, 7.544268491722988 51.24810327804511, 7.544268491722988 51.70379817037631, 6.475457372470345 51.7037981703763))"^^geo:wktLiteral
  }
  LATERAL { SERVICE <cache:> {
    SELECT * {
      ?x spatial:intersectBoxGeom(?searchGeom) .
      ?x wdt:P31 wd:Q4830453 . # business
      ?x geo:hasGeometry/geo:asWKT ?geom .
      FILTER(geof:sfIntersects(?geom, ?searchGeom))
      LATERAL {
        SELECT ?x ?label {
          VALUES ?prefLang { "de" "en" }
          ?x rdfs:label ?label .
          FILTER(langMatches(lang(?label), ?prefLang))
        } LIMIT 1
      }
      ?x wdt:P1128 ?rawEmployees
      # OPTIONAL { ?x wdt:P1128 ?rawEmployees }
      BIND(if(bound(?rawEmployees), ?rawEmployees, 'unspecified') AS ?employees)

      BIND(STRDT('<p><a href="' + STR(?x) + '">' + STR(?x) + '</a>: ' + STR(?label) + ' - ' + STR(?employees) + '</p>', rdf:HTML) AS ?geomTooltip)
      BIND(?geomTooltip AS ?geomLabel)
    }
  } }
}

