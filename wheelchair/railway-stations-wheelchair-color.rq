# YasGui Link: https://api.triplydb.com/s/DA90SQxJm

PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX osmt: <https://www.openstreetmap.org/wiki/Key:>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?s ?label ?wheelchair ?railway ?wkt ?wktColor ?wktLabel WHERE {
   # Prepend '#' to the next line to search all regions.
   BIND("POLYGON ((6.475457372470345 51.7037981703763, 6.475457372470346 51.2481032780451, 7.544268491722988 51.24810327804511, 7.544268491722988 51.70379817037631, 6.475457372470345 51.7037981703763))"^^geo:wktLiteral AS ?area)
   
#  GRAPH <https://data.mobydex.org/infrastructure/railway/> { 
    ?s osmt:railway "station" .
    ?s geo:hasGeometry ?geom .    
    ?geom geo:asWKT ?wkt .
    FILTER(!bound(?area) || geof:sfIntersects(?wkt, ?area))
    OPTIONAL { ?s osmt:wheelchair ?wheelchair }
    OPTIONAL { ?s osmt:name ?label }
    OPTIONAL { ?s osmt:railway ?railway }
#  }
  
  BIND(if(!bound(?wheelchair), "black",
         if(?wheelchair = "yes", "green",
           if(?wheelchair = "no", "red", "orange"))) AS ?wktColor)
  
  # create tooltip
  BIND(strdt(concat(
    '<h4>', STR(coalesce(?label, ?s)), '</h4>',
    '<a href="', STR(?s), '" target="_blank">', STR(?s), '</a>',
    '<p><b>Wheelchair</b> ',str(coalesce(?wheelchair, 'not set')), '</p>',
    '<p><b>Railway</b> ',str(coalesce(?railway, 'not set')), '</p>'
  ), rdf:HTML) as ?wktLabel)
}
# LIMIT 1000
